#Filmographie Maciste https://w.wiki/BtLX
#Filmemacher in einer Zeile
# Werte: Filmtitel, Jahr der Erstaufführung, Filemacher, Produktionsland
SELECT 
(?movie AS ?WikidataID) # wikidata ID
(?movieLabel AS ?Filmtitel) # Filmtitel in [AUTO LANGUAGE], sofern vorhanden
(SAMPLE (YEAR(?publicationDate)) AS ?Datum)
(group_concat(?directorLabel; separator=', ') AS ?Filmemacher) # Regisseur:innen, als Liste
(group_concat( distinct ?countryLabel; separator=', ') AS ?Land) # Produktionsländer, als Liste

WHERE
{
wd:Q332436 wdt:P1441 ?movie.
?movie wdt:P57 ?director.

OPTIONAL {
  ?movie wdt:P495 ?country.
  }

OPTIONAL {
  ?movie wdt:P577 ?publicationDate.
  }

SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". 
                         ?movie rdfs:label ?movieLabel.
                         ?director rdfs:label ?directorLabel.
                         ?country rdfs:label ?countryLabel.
                         }
}

GROUP BY ?movie ?movieLabel ?Datum ?Filmemacher ?Land # nur eine Zeile pro Werk ausgeben, mehrfache Werte (Filemmacher, Land) sind kommagetrennte Liste
ORDER BY (?Datum) # nach Publikationsdatum (P577) aufsteigend sortiert
